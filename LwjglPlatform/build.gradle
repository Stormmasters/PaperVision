plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: '../build.common.gradle'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation project(":EasyVision")
    implementation("com.github.deltacv.EOCV-Sim:Common:$eocvsim_common_version") {
        exclude group: 'org.openpnp', module: 'opencv'
    }

    implementation "io.github.spair:imgui-java-app:$imgui_version"

    implementation platform('org.lwjgl:lwjgl-bom:3.2.3')
    implementation "org.lwjgl:lwjgl-stb"

    ['natives-linux', 'natives-windows', 'natives-macos'].each {
        implementation "org.lwjgl:lwjgl-stb::$it"
    }
}

task(runEv, dependsOn: 'classes', type: JavaExec) {
    main = 'io.github.deltacv.easyvision.platform.lwjgl.AppMain'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.withType(Jar) {
    manifest {
        attributes['Main-Class'] = 'io.github.deltacv.easyvision.platform.lwjgl.AppMain'
    }
}